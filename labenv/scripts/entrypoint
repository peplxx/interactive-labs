#!/usr/bin/fish
# shellcheck disable=SC1071

# Startup script executes once as the web terminal is loaded
set -Ux fish_greeting ""

# Run if in docker container
if test -f /.dockerenv

    # if in sysbox container, run recording script directly
    if type systemctl 2>/dev/null
        exec fish /app/scripts/record.fish
    end

    # Otherwise, assume docker socket is mapped
    alias -s docker "sudo docker"
    set -Ux HOST_WORKDIR "$(docker inspect labenv | jq -r '.[0].Mounts[] | select(.Destination=="/home/ubuntu") | .Source')"
    set -Ux HOST_USERNAME "$(docker system info --format '{{.Name}}')"
    set -Ux NETWORK_GATEWAY "$(docker inspect lab_net | jq -r '.[0].IPAM.Config[0].Gateway')"
    set -Ux OS_ID $(grep '^ID=' /etc/os-release | cut -d= -f2)

    # If in debian container, SSH to VM host. Otherwise run directly inside container
    if test "$OS_ID" = "debian"
        sshpass -p 'vagrant' scp -p -o StrictHostKeyChecking=no /app/scripts/record.fish vagrant@"$NETWORK_GATEWAY":/tmp/record.fish
        exec sshpass -p 'vagrant' ssh -o StrictHostKeyChecking=no -t vagrant@"$NETWORK_GATEWAY" "exec fish /tmp/record.fish"
    else
        exec fish /app/scripts/record.fish
    end
end

# Local development environment
exec fish 
